buildscript {
    def config = new Properties()
    file('build.properties').withInputStream(config.&load)
    project.ext.config = config

    repositories {
        mavenCentral()
        google()
    }
}

apply plugin: 'java'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

archivesBaseName = config.project_id
group = config.project_group
version = "${config.project_version}.${(System.getenv("CI_PIPELINE_IID") ?: 0)}"
sourceCompatibility = config.java_version
targetCompatibility = config.java_version

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation gradleApi()
    implementation group: 'org.jetbrains', name: 'annotations', version: config.annotations_version
}

gradlePlugin {
    plugins {
        cmakePlugin {
            id = "${config.project_group}.${config.project_id}"
            implementationClass = "${config.project_group}.CMakePlugin"
        }
    }
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = config.java_version
    targetCompatibility = config.java_version
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASSWORD')) {
            maven {
                url "https://nexus.covers1624.net/repository/karmakrafts-releases/"

                credentials {
                    username 'kitsunealex'
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
    publications {
        gradlecm(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            //artifact jar
            //artifact shadowJar
            //artifact sourcesJar

            pom {
                name = archivesBaseName
                description = archivesBaseName
                //The publish plugin doesnt like GString's here apparently..
                url = "https://git.karmakrafts.dev/kk/gradle-cmake".toString()

                scm {
                    url = "https://git.karmakrafts.dev/kk/gradle-cmake".toString()
                }

                issueManagement {
                    system = 'gitlab'
                    url = "https://git.karmakrafts.dev/kk/gradle-cmake/issues".toString()
                }

                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://git.karmakrafts.dev/kk/gradle-cmake/-/blob/master/LICENSE"
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'kitsunealex'
                        name = 'KitsuneAlex'
                    }
                    developer {
                        id = 'freudi74'
                        name = 'Marco Freudenberger'
                    }
                }
            }
        }
    }
}
